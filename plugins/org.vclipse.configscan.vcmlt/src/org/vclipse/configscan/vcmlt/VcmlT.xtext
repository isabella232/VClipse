grammar org.vclipse.configscan.vcmlt.VcmlT with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.vclipse.vcml.mm/src/org/vclipse/vcml/mm/VCML.ecore" as VCML

generate vcmlT "http://www.vclipse.org/configscan/vcmlt/VcmlT"

// ToDo
// MV Cstics

Model
	:	imports+=Import*
	    testcase=TestCase
		testgroups+=TestGroup*
	;
	
Import
	:	'import' importURI=STRING
	;

// ToDo: define version and part syntax more exactly
TestCase
	:	'testcase' item=[VCML::Material|EXTENDED_ID]
		('['	(
					('document' document=STRING)
				&	('description' description=STRING)
				&	('version' version=STRING)?
				&	('part' part=STRING)?
				)
        ']')?
	;

TestGroup
	:	'testgroup' name=EXTENDED_ID (description=STRING)?
        ('['    (   ('status' status=Status)?
                &   ('mode' mode=Mode)?
                )
         ']')?
		
		'{'
			actions+=Action*
		'}'
	;

enum Status
    :   released // default value - must be first element
    |   inPreparation
    |   locked
    ;
    	
enum Mode
	:   success // default value - must be first element
	|	failure
	;	
	
Action
    :   SetValue
    |   CheckSingleValue
    |   CheckStatus
    |   CheckDomain
    |   CheckConflict
    |   CheckComplete
    |   CheckBomItemQty
    |   CheckBomItemExists
    |   CheckBomCountItems
    |   TestGroup
	;

SetValue
	:	cstic=[VCML::Characteristic] '=' value=Literal ('@' bompath=BomPath)?
	;

// ToDo: include setting for author if reasonable as in cmlt	
CheckSingleValue
	:	'check' cstic=[VCML::Characteristic] ('=' value=SymbolicLiteral)? ('@' bompath=BomPath)?
	|	'check' cstic=[VCML::Characteristic] operator=Operator value=NumericLiteral ('@' bompath=BomPath)?
	|	'check' cstic=[VCML::Characteristic] 'in' value=NumericInterval ('@' bompath=BomPath)?
	;

CheckStatus
    :   'check' cstic=[VCML::Characteristic] status+=CsticState+ ('@' bompath=BomPath)?
    ;
	
CheckDomain
    :   'checkDomain' strict?='strict'? cstic=[VCML::Characteristic] '{'
            values+=DomainValue*
        '}' ('@' bompath=BomPath)?
    ;

DomainValue
    :   no?='no'? literal=Literal
    ;
    	
CheckConflict
    :   {CheckConflict}
        'check' (conflict?='inconsistent' | 'consistent') ('@' bompath=BomPath)?
    ;
    
CheckComplete
    :   {CheckComplete}
        'check' (complete?='complete' | 'incomplete') ('@' bompath=BomPath)?
    ;
	
enum CsticState
    :   visible
    |   invisible = 'hidden'            // 'hidden' reserved by xtext
    |   enabled
    |   disabled
    |   set
    |   unset
    ;
	
enum Operator
    :   LT='<'
    |   LE='<='
    |   NE='!='  // ToDo: Doublecheck ConfigScan syntax
    |   EQ='='
    |   GE='>='
    |   GT='>'
    ;
    
NumericInterval
	:
		'(' '>'? value=NumericLiteral '-' '<'? value=NumericLiteral ')'  // ToDo: Doublecheck ConfigScan syntax, and what makes sense here
	;
    
NUMBER
    :   INT ('.' INT)?
    ;
    
EXTENDED_ID
    :   ID | SYMBOL
    ;
    
Literal returns VCML::Literal
    :   NumericLiteral
    |   SymbolicLiteral
    ;
    
NumericLiteral returns VCML::NumericLiteral
    :   value=NUMBER
    ;

SymbolicLiteral returns VCML::SymbolicLiteral
    :   value=SYMBOL
	;
    
terminal STRING // Xtext standard, but only enclosed in double quotes
    :   '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|'\\') | !('\\'|'"') )* '"'
    ;

terminal SYMBOL // enclosed in single quotes, no escapes, no line breaks, no tabs
    :   "'" ( !("'"|'\t'|'\n'|'\r') )* "'"
    ; 

// ToDo: apply also to other bom item types	
CheckBomItemQty
    :    'check' 'quantity' operator=Operator amount=INT ('@' bompath=BomPath)?
    ;        
		

// ToDo: should only apply to materials with bom
CheckBomCountItems
	:	'check' 'countitems' '=' value=INT ('@' bompath=BomPath)?		
	;
	
CheckBomItemExists
	:	'check' (exists?='exists' | 'notexists') ('@' bompath=BomPath)?		
	;

BomPath
	:	{BomPath}
		'/' ('(' position=INT ')')? material=[VCML::Material|EXTENDED_ID]? child=BomPath?
	;